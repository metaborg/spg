module predef

imports ds-signatures/Tiger-sig
imports dynamics/base
imports dynamics/natives
imports dynamics/store
imports dynamics/bindings
imports dynamics/functions

signature

  constructors
    nativePrintV     : Exp
    nativeOrdV       : Exp
    nativeChrV       : Exp
    nativeNotV       : Exp
    nativeExitV      : Exp
    nativeSizeV      : Exp
    nativeSubstringV : Exp
    nativeConcatV    : Exp
    nativeGetcharV   : Exp
    nativeFlushV     : Exp
  
  arrows
    E |- stdLib() :: H --> Env :: H

rules

  // predefined functions  
  E |- stdLib() --> E'
  where
    E  |- bindVar("print",     ClosureV([FArg("s", Tid("string"))], nativePrintV(), E)) --> E1;
    E1 |- bindVar("ord",       ClosureV([FArg("s", Tid("string"))], nativeOrdV(), E)) --> E2;
    E2 |- bindVar("chr",       ClosureV([FArg("i", Tid("int"))], nativeChrV(), E)) --> E3;
    E3 |- bindVar("not",       ClosureV([FArg("i", Tid("int"))], nativeNotV(), E)) --> E4;
    E4 |- bindVar("exit",      ClosureV([FArg("i", Tid("int"))], nativeExitV(), E)) --> E5;
    E5 |- bindVar("size",      ClosureV([FArg("s", Tid("string"))], nativeSizeV(), E)) --> E6;
    E6 |- bindVar("substring", ClosureV([FArg("s", Tid("string")), FArg("i1", Tid("int")), FArg("i2", Tid("int"))], nativeSubstringV(), E)) --> E7;
    E7 |- bindVar("concat",    ClosureV([FArg("s1", Tid("string")), FArg("s2", Tid("string"))], nativeConcatV(), E)) --> E8;
    E8 |- bindVar("getchar",   ClosureV([], nativeGetcharV(), E)) --> E9;
    E9 |- bindVar("flush",     ClosureV([], nativeFlushV(), E)) --> E'.
  
  // predefined function reductions
  nativePrintV() --> UnitV()
  where
    read(lookup("s")) => StringV(s);
    printS(s) => _.

  nativeOrdV() --> IntV(result)
  where
    read(lookup("s")) => StringV(s);
    ordS(s) => result.

  nativeChrV() --> StringV(result)
  where
    read(lookup("i")) => IntV(i);
    chrI(i) => result.
    
  nativeNotV() --> IntV(result)
  where
    read(lookup("i")) => IntV(i);
    notI(i) => result.
  
  nativeExitV() --> UnitV()
  where
    read(lookup("i")) => IntV(i);
    notI(i) => _.
    
  nativeSizeV() --> IntV(result)
  where
    read(lookup("s")) => StringV(s);
    sizeS(s) => result.
  
  nativeSubstringV() --> StringV(result)
  where
    read(lookup("s")) => StringV(s);
    read(lookup("i1")) => IntV(i1);
    read(lookup("i2")) => IntV(i2);
    substringSII(s, i1, i2) => result.
  
  nativeConcatV() --> StringV(result)
  where
    read(lookup("s1")) => StringV(s1);
    read(lookup("s2")) => StringV(s2);
    concatS(s1, s2) => result.
  
  nativeGetcharV() --> StringV(result)
  where
    getcharS("") => result.
  
  nativeFlushV() --> UnitV()
  where
    flushS("") => _.
  