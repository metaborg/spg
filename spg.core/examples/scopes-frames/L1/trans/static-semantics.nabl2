module static-semantics

imports

  signatures/-
  
signature
  types
    TInt()
    TFun(type, type)
  
rules

  init ^ (s) : ty :=
    new s.
    
  [[ IntValue(_) ^ (_) : TInt() ]] :=
    true.
  
  [[ Var(x) ^ (s) : t ]] :=
    Var{x} -> s,
    Var{x} |-> d,
    d : t.
  
  [[ Add(e1, e2) ^ (s) : TInt() ]] :=
    [[ e1 ^ (s) : TInt() ]],
    [[ e2 ^ (s) : TInt() ]].

  [[ Fun(x, t, e) ^ (s) : TFun(t1, t2) ]] :=
    Var{x} <- s',
    Var{x} : t1,
    [[ t ^ () : t1 ]],
    [[ e ^ (s') : t2 ]],
    s' ---> s,
    new s'.

  [[ App(e1, e2) ^ (s) : t2 ]] :=
    [[ e1 ^ (s) : TFun(t1, t2) ]],
    [[ e2 ^ (s) : t1 ]].
    
  [[ IntType() ^ () : TInt() ]].
    
  [[ FunType(t1, t2) ^ () : TFun(t1', t2') ]] :=
    [[ t1 ^ () : t1' ]],
    [[ t2 ^ () : t2' ]].
