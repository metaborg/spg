module L2

context-free start-symbols
  
  Start Exp Declaration Field Type

context-free syntax
  
  Start.Program = <<{Declaration "\n\n"}*>

begin
  <Exp>
end>

context-free syntax // Exp

  Exp.IntValue = INT
  
  Exp.Add = <<Exp> + <Exp>> {left}
  
  Exp.Fun = <fun(<ID>: <Type>) {
  <Exp>
}>

  Exp.App = <<Exp>(<ResetExp>)>
  
  // Hack to allow e.g. addition as argument to an application
  ResetExp = Exp
  
  Exp = Lhs
  
  Exp.NewObject = <new <ID>>
  
  Exp.Assign = <<Lhs> := <Exp>> {left}
  
  Exp.Seq = <<Exp>; <Exp>> {left}
  
  Lhs.Var = ID
  
  Lhs.QVar = <<Exp>.<ID>> {left}
  
  Exp = <(<Exp>)> {bracket}

context-free syntax // Declaration
  
  Declaration.Record = <record <ID> {
  <{Field "\n"}*>
}>
  
context-free syntax // Field

  Field.Field = <<ID>: <Type>>

context-free syntax // Types

  Type.IntType = <Int>
  
  Type.FunType = <<Type> -\> <Type>> {right}

  Type.RecType = ID
  
context-free priorities

  Exp.App > Exp.Add,
  Exp.Add > Exp.Seq,
  Exp.Seq > Exp.Assign,
  Lhs.QVar > Exp.Seq
  
lexical syntax

  ID             = [a-zA-Z] [a-zA-Z0-9]* 
  INT            = "-"? [0-9]+ 
  STRING         = "\"" StringChar* "\"" 
  StringChar     = ~[\"\n] 
  StringChar     = "\\\"" 
  StringChar     = BackSlashChar 
  BackSlashChar  = "\\" 
  LAYOUT         = [\ \t\n\r] 
  CommentChar    = [\*] 
  LAYOUT         = "/*" InsideComment* "*/" 
  InsideComment  = ~[\*] 
  InsideComment  = CommentChar 
  LAYOUT         = "//" ~[\n\r]* NewLineEOF 
  NewLineEOF     = [\n\r] 
  NewLineEOF     = EOF 
  EOF            =  

lexical restrictions

  // Ensure greedy matching for lexicals
  
  CommentChar   -/- [\/]
  INT           -/- [0-9]
  ID            -/- [a-zA-Z0-9\_]
  
  // EOF may not be followed by any char
  
  EOF           -/- ~[]
  
  // Backslash chars in strings may not be followed by " 
  
  BackSlashChar -/- [\"]

context-free restrictions

  // Ensure greedy matching for comments
  
  LAYOUT? -/- [\ \t\n\r]
  LAYOUT? -/- [\/].[\/]
  LAYOUT? -/- [\/].[\*]

lexical syntax

  // Reserved characters
  
  ID = "Int" {reject}
  